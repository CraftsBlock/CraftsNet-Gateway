plugins {
    id 'java'
    id 'maven-publish'
}

def env = new Properties()
file(".env").withInputStream {
    env.load(it)
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withJavadocJar()
    withSourcesJar()
}

group = 'de.craftsblock.craftsnet.modules'
version = '1.0.0-pre3'

repositories {
    mavenCentral()
    maven { url = "https://repo.craftsblock.de/releases" }
    maven { url = "https://repo.craftsblock.de/experimental" }
}

dependencies {
    // CraftsBlock related dependencies ---------------------------------------------------------------------------------------

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/bom
    implementation platform("de.craftsblock.craftscore:bom:3.8.11")

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/utils
    implementation "de.craftsblock.craftscore:utils"

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/cache
    implementation "de.craftsblock.craftscore:cache"

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/event
    implementation "de.craftsblock.craftscore:event"

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftscore/json
    implementation "de.craftsblock.craftscore:json"

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftsnet
    implementation "de.craftsblock:craftsnet:3.5.2"

    // https://repo.craftsblock.de/#/releases/de/craftsblock/craftsnet/modules/streamencoders
    implementation "de.craftsblock.craftsnet.modules:streamencoders:1.0.1"

    // Third party dependencies ----------------------------------------------------------------------------------------

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation 'org.jetbrains:annotations:26.0.2-1'
}

publishing {
    publications {
        normal(MavenPublication) {
            artifactId = "gateway"
            from components.java
            pom {
                name = 'Gateway'
                description = 'Simple proxy service for craftsnet'

                scm {
                    url = 'https://github.com/CraftsBlock/CraftsNet-Gateway'
                    connection = 'scm:git:git://github.com/CraftsBlock/CraftsNet-Gateway.git'
                    developerConnection = 'scm:git:git@github.com:CraftsBlock/CraftsNet-Gateway.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/CraftsBlock/CraftsNet-Gateway/issues'
                }

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://github.com/CraftsBlock/CraftsNet-Gateway/blob/master/LICENSE'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url('https://repo.craftsblock.de/experimental')
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = env["username"]
                password = env["password"]
            }
        }
    }
}
